apply plugin: 'com.android.application'

apply from: "$project.rootDir/tools/findbugs.gradle"
apply from: "$project.rootDir/tools/checkstyle.gradle"
apply from: "$project.rootDir/tools/pmd.gradle"

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.a65apps.architecturecomponents"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file("$propStoreFile")
            storePassword "$propStorePassword"
            keyAlias "$propKeyAlias"
            keyPassword "$propKeyPassword"
        }
        release {
            storeFile file("$propStoreFileRelease")
            storePassword "$propStorePassword"
            keyAlias "$propKeyAliasRelease"
            keyPassword "$propKeyPassword"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warningsAsErrors true
    }
}

dependencies {
    def autoValueParcelVersion = '0.2.6'
    def autoValueGsonVersion = '0.7.0'
    def butterKnifeVersion = '8.8.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':architecturecomponents')
    implementation project(':ciceronearchitecturecomponents')
    implementation project(':moxyarchitecturecomponents')
    implementation project(':daggerarchitecturecomponents')
    implementation project(':moxydaggerarchitecturecomponents')

    // Support
    implementation "com.android.support:appcompat-v7:$project.ext.supportVersion"
    implementation "com.android.support:recyclerview-v7:$project.ext.supportVersion"
    implementation "com.android.support:design:$project.ext.supportVersion"

    // Dagger
    implementation "com.google.dagger:dagger:$project.ext.daggerVersion"
    implementation ("com.google.dagger:dagger-android-support:$project.ext.daggerVersion") {
        exclude group: "com.android.support"
    }
    annotationProcessor "com.google.dagger:dagger-compiler:$project.ext.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$project.ext.daggerVersion"

    // MVP dependencies
    implementation "com.arello-mobile:moxy:$project.ext.moxyVersion"
    implementation "com.arello-mobile:moxy-app-compat:$project.ext.moxyVersion"
    annotationProcessor "com.arello-mobile:moxy-compiler:$project.ext.moxyVersion"

    // AutoValue
    compileOnly "com.ryanharter.auto.value:auto-value-gson:$autoValueGsonVersion"
    annotationProcessor "com.ryanharter.auto.value:auto-value-parcel:$autoValueParcelVersion"
    annotationProcessor "com.ryanharter.auto.value:auto-value-gson:$autoValueGsonVersion"

    // ButterKnife dependencies
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$project.ext.rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$project.ext.rxAndroidVersion"

    // Javax.inject annotations
    implementation "javax.inject:javax.inject:$project.ext.injectVersion"

    // Cicerone
    implementation "ru.terrakok.cicerone:cicerone:$project.ext.ciceroneVersion"

    // JCIP dependencies
    implementation "net.jcip:jcip-annotations:$project.ext.jcipVersion"

    // test dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
