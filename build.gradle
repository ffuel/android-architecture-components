// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.2'
        

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "org.sonarqube" version "2.6"
}

def projectVersion = '1.0.10'

sonarqube {
    properties {
        property "sonar.sources", "src"
        property "sonar.java.source", "7"
        property "sonar.projectKey", "architecture-components"
        property "sonar.projectVersion", projectVersion
        property "sonar.findbugs.excludesFilters", "$project.rootDir/tools/rules-findbugs.xml"
    }
}

project(":compiler") {
    sonarqube {
        properties {
            property "sonar.java.binaries", "build/classes/java"
            property "sonar.jacoco.reportPaths", "build/jacoco/test.exec"
            property "sonar.junit.reportPaths", "build/test-results/test"
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    project.ext {
        supportVersion = '27.1.1'
        rxJavaVersion = '2.1.12'
        rxAndroidVersion = '2.0.2'
        jcipVersion = '1.0'
        injectVersion = '1'
        moxyVersion = '1.5.3'
        ciceroneVersion = '3.0.0'
        daggerVersion = '2.11'
        javapoetVersion = '1.9.0'
        autoServiceVersion = '1.0-rc2'

        // Test
        junitVersion = '4.12'
        mockitoVersion = '2.18.3'
        espressoVersion = '3.0.2'
        testRunnerVersion = '1.0.2'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
